cmake_minimum_required(VERSION 3.14)

set_property(GLOBAL PROPERTY USE_FOLDERS ON)

cmake_policy(SET CMP0091 NEW)

project("DisplayInfo" VERSION "0.0.3" LANGUAGES CXX)

set(DISPLAYINFO_VERSION_TAG "")
if(DEFINED DISPLAYINFO_RELEASE_TAG AND NOT "${DISPLAYINFO_RELEASE_TAG}" STREQUAL "")
    set(DISPLAYINFO_VERSION_TAG "${DISPLAYINFO_RELEASE_TAG}")
else()
    set(DISPLAYINFO_VERSION_TAG "v${PROJECT_VERSION}")
endif()

if(NOT DISPLAYINFO_VERSION_TAG MATCHES "^v")
    set(DISPLAYINFO_VERSION_TAG "v${DISPLAYINFO_VERSION_TAG}")
endif()

execute_process(
    COMMAND git rev-parse --short=12 HEAD
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    OUTPUT_VARIABLE DISPLAYINFO_GIT_COMMIT
    RESULT_VARIABLE DISPLAYINFO_GIT_RESULT
    OUTPUT_STRIP_TRAILING_WHITESPACE
)

if(NOT DISPLAYINFO_GIT_RESULT EQUAL 0 OR "${DISPLAYINFO_GIT_COMMIT}" STREQUAL "")
    set(DISPLAYINFO_GIT_COMMIT "unknown")
endif()

string(TIMESTAMP DISPLAYINFO_BUILD_TIMESTAMP "%Y-%m-%dT%H:%M:%S%z")

set(CMAKE_CONFIGURATION_TYPES
    "Debug"
    "Release"
    CACHE STRING "" FORCE
)

if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE "Release" CACHE STRING
        "Choose the type of build, options are: Debug Release."
        FORCE)
endif()

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

option(STATIC_CRT "Static runtime" ON)

add_executable(${PROJECT_NAME} DisplayInfo.cpp "${CMAKE_CURRENT_SOURCE_DIR}/DisplayConfig/DisplayConfig.cpp")
target_link_libraries(${PROJECT_NAME} PRIVATE Shcore dxgi)
target_compile_definitions(${PROJECT_NAME} PRIVATE
    DISPLAYINFO_VERSION_TAG="${DISPLAYINFO_VERSION_TAG}"
    DISPLAYINFO_GIT_COMMIT="${DISPLAYINFO_GIT_COMMIT}"
    DISPLAYINFO_BUILD_TIMESTAMP="${DISPLAYINFO_BUILD_TIMESTAMP}"
)

target_include_directories(${PROJECT_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CMAKE_CURRENT_SOURCE_DIR}/third_party
)

if(MSVC)
    if(STATIC_CRT)
        set_target_properties(${PROJECT_NAME} PROPERTIES
            MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>"
        )
    endif()

    set_target_properties(${PROJECT_NAME} PROPERTIES LINK_FLAGS "/DELAYLOAD:api-ms-win-shcore-scaling-l1-1-1.dll /DELAYLOAD:user32.dll")
    set_target_properties(${PROJECT_NAME} PROPERTIES RUNTIME_OUTPUT_DIRECTORY_RELEASE ${PROJECT_BINARY_DIR}/$<0:>)
    set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${PROJECT_NAME})
else()
    if(STATIC_CRT)
        target_link_options(${PROJECT_NAME} PRIVATE "-static")
    endif()
endif()
